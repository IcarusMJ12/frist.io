"civet autoLet"

EMOJI_OFFSET := 3

data := await chrome.storage.local.get()

col_names := []
for header of $ '#reactions-table > thead > tr > th > div'
  col_names.push header.innerHTML
body := $('#reactions-table-body').first()

rows := []
for [ url, values ] of Object.entries data
  [ date, title, reactions ] := values
  row := [ url, title, date ]
  for i = 0; i < 16; i++
    row.push if reactions & (1 << i) then col_names[i] else ''
  rows.push row

filters := [] // 0 -> all, 1 -> present, -1 -> absent
emoji_indices := []
for i = 0; i < 16; i++
  filters.push 0
  emoji_indices.push i + EMOJI_OFFSET

filter_rows = =>
  filtered = rows
  for filter, index of filters
    if filter == 1
      filtered = filtered.filter((row) => row[index + EMOJI_OFFSET] != '')
    else if filter == -1
      filtered = filtered.filter((row) => row[index + EMOJI_OFFSET] == '')
  return filtered

table := new DataTable '#reactions-table',
  columnDefs: [ orderable: false, targets: emoji_indices
    className: 'clickable', targets: [ 0 ]]
  data: rows
  layout: topStart: pageLength: menu: [ 50, 100, 200, 400 ]
  pageLength: 100

$('#reactions-table > thead > tr > th').on 'click', (evt) =>
  index := evt.currentTarget.cellIndex - EMOJI_OFFSET
  switch filters[index]
    when 0
      filters[index] = 1
      evt.target.className = 'selected'
    when 1
      filters[index] = -1
      evt.target.className = 'deselected'
    else
      filters[index] = 0
      evt.target.className = ''
  table.clear()
  table.rows.add filter_rows()
  table.draw()

$('#reactions-table').on 'click', 'tbody tr', (evt) =>
  window.open table.row(evt.target).data()[0], '_blank'
