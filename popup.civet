formatter := new Intl.NumberFormat 'en', notation: 'compact'
get_parameters :=
  'www.youtube.com/watch': ['v']
tab := (await chrome.tabs.query({ active: true, lastFocusedWindow: true }))[0]
canonized_url := canonizeUrl tab.url

REACTIONS := 0
TITLE := 1
DATE := 2

function canonizeUrl(url_)
  prefix := url_.split('?')[0]
  params := new URLSearchParams()
  u := new URL url_
  for key of get_parameters[u.host + u.pathname] ?? []
    p := u.searchParams.get(key)
    if p === undefined
      continue
    params.append(key, p)
  param_string := params.toString()
  return if param_string.# then [prefix, param_string].join('?') else prefix

stored := await chrome.storage.local.get canonized_url
let reactions = stored[canonized_url][REACTIONS] ?? 0

/*
emoji_names := ['true', 'false',
                'fire', 'chill',
                'happy', 'sad',
                'quality', 'shit',
                'informative', 'confusing',
                'funny', 'boring',
                'troll', 'shocked',
                'wholesome', 'disgusting']
*/

function get_reaction(index)
  return reactions & (1 << index)

function toggle_reaction(index)
  reactions ^= (1 << index)
  target := new Object()
  target[canonized_url] = [reactions, tab.title.slice(0, 60), new Date().toISOString()]
  chrome.storage.local.set(target).then(() => chrome.storage.local.get(canonized_url).then((result) => console.log(result)));

function react(evt, index)
  toggle_reaction(index)
  element := evt.currentTarget
  count := element.getElementsByClassName('reaction-count')[0]
  element.className = if get_reaction(index) then 'reacted' else ''
  element.getElementsByClassName('reaction-count')[0].innerText =
    formatter.format count.value + (if get_reaction(index) then 1 else 0)

for element, index of document.getElementById('reactions').children
  count := element.getElementsByClassName('reaction-count')[0]
  count.value = 0  // TODO
  element.className = if get_reaction(index) then 'reacted' else ''
  element.addEventListener('click', (evt) => react(evt, index))
  element.getElementsByClassName('reaction-count')[0].innerText =
    formatter.format count.value + (if get_reaction(index) then 1 else 0)
